/**
 * @description 컨텐트 정보를 MART 로 부터 가져온다.
 */
Concepts.ECF.SCENARIO.ContentInfo rulefunction RuleFunctions.ECF.MART.getContentInfoFromMart {
	attribute {
		validity = ACTION;
	}
	scope {
		String albumId;	
	}
	body {
		Object logger = Log.getLogger(ScoreCard.log_control.DB_TAG + ":getContentInfoFromMart");
		Object conn = null;
		Object res = null;
		try{
			conn = Oracle.getConnection("MasterContent");
			String query = null;
			if(System.getGlobalVariableAsString("COMMON/MART_MODE", "real") == "real"){
				query = "SELECT * FROM TB_CMCE_REBM_BRD_CNTS_M WHERE ALBM_ID = ?";
			}else{
				query = "SELECT * FROM TB_CMCE_REBM_BRD_CNTS_M_T WHERE ALBM_ID = ?";
			}

			Object[] params = {albumId}; //for test
			res = Oracle.executeQuery(conn, query, params);
			Concepts.ECF.SCENARIO.ContentInfo ciInstance = null;
			if(res!= null){
				while(Oracle.next(res)){
					if(ciInstance == null){
						ciInstance = Concepts.ECF.SCENARIO.ContentInfo.ContentInfo(
										albumId/*extId String */,
										albumId/*CID String */,
										null/*TITLE String */,
										null/*CREATED DateTime */,
										getTrimString(Oracle.getColumnValueByName(res, "CNTS_GNRE_NM_LIST_CNTN"))/*GENRE String */,
										getTrimString(Oracle.getColumnValueByName(res, "CNTS_CATG_ID_LIST_CNTN"))/*CATEGORY String */,
										getTrimString(Oracle.getColumnValueByName(res, "MFA_PROD_LIST_CNTN"))/*CODE String */,
										getLongValueOnDB(Oracle.getColumnValueByName(res, "PROD_PRC"))/*PRICE int */,
										null/*SERIES_MODE String */,
										0/*EPISODE_NUM int */,
										null/*SVC_NAME String */,
										null/*AVAIL_ET DateTime */);
						return ciInstance;
					}
				}
			}
			return ciInstance;
		}catch(Exception e){
			Log.log(logger, "Error", "Exception:"+ e);
			if(conn != null) Oracle.rollback(conn);
			return null;
		}finally{
			if(res != null) Oracle.closeResultSet(res);
			if(conn != null) Oracle.releaseConnection(conn);
		}
	}
}